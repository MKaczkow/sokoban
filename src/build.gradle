plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'elkaproj'
version '1.0-SNAPSHOT'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

compileJava.sourceCompatibility = "1.8"
compileJava.targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

dependencies {
    compile "javax.xml.bind:jaxb-api:2.3.0"
    compile "com.sun.xml.bind:jaxb-core:2.3.0"
    compile "com.sun.xml.bind:jaxb-impl:2.3.0"
    compile "com.sun.activation:javax.activation:1.2.0"

    implementation project(":modules:common")

    testCompile "junit:junit:4.12"

    testImplementation project(":modules:common")
    testImplementation project(":modules:http-server")
}

mainClassName = 'elkaproj.Entry'

jar {
    manifest {
        attributes(
                "Main-Class": mainClassName
        )
    }
}

subprojects {
    apply plugin: "java"
}

task javadocAll(type: Javadoc) {
    def exportedProjects = [
            project.path,
            ":modules:common",
            ":modules:http-server"
    ]

    description = "Generates Javadoc of all the projects in the workspace"

    source exportedProjects.collect {
        project(it).sourceSets.main.allJava
    }

    classpath = files(exportedProjects.collect {
        project(it).sourceSets.main.compileClasspath
    })

    destinationDir = file("${buildDir}/docs/javadoc-all")
}
